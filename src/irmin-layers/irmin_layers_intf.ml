(*
 * Copyright (c) 2018-2021 Tarides <contact@tarides.com>
 *
 * Permission to use, copy, modify, and distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 *)

type layer_id = [ `Upper0 | `Upper1 | `Lower ] [@@deriving irmin]

module type Generic_key = sig
  include Irmin.Generic_key.S

  val freeze :
    ?min_lower:commit list ->
    ?max_lower:commit list ->
    ?min_upper:commit list ->
    ?max_upper:commit list ->
    ?recovery:bool ->
    repo ->
    unit Lwt.t
  (** [freeze ?min_lower ?max_lower ?min_upper ?max_upper ?recovery t] launches
      an asynchronous freezing operation on the repo [t] to reduce the size of
      the upper layer and discard unnecessary branches of objects (i.e. commits,
      nodes and contents).

      Let [o] be the set of objects reachable from the [max_lower] commits and
      bounded by the [min_lower] commits. During the freeze, all objects in [o]
      are copied to the lower layer, if there is one. [max_lower] defaults to
      the head commits of the repo and [min_lower] defaults to the empty list
      (i.e. the copy is unbounded). When [max_lower] is the empty list, nothing
      is copied.

      Let [o'] be the set of objects reachable from the [max_upper] commits and
      bounded by the [min_upper] commits. When the freeze is over, the new upper
      layer will only contain the objects of [o']. [max_upper] defaults to
      [max_lower] and [min_upper] defaults to [max_upper] (i.e. only the max
      commits are copied). When [max_upper] is the empty list, nothing is
      copied.

      If [recovery] is true then the function will first try to recover from a
      previously interrupted freeze. See {!needs_recovery}.

      If a freeze is already ongoing, the behavior depends on the
      freeze_throttle configuration of the repo:

      - When [`Overcommit_memory], the function returns without launching a new
        freeze.
      - When [`Cancel_existing], the function blocks until the ongoing freeze
        safely cancels and then a new one is started afterwards. The time spent
        doing the canceled freeze is not completely wasted, objects copied to
        the lower layer will not have to be copied again, but objects copied to
        the next upper layer are discarded from that layer.
      - When [`Block_writes], the function blocks until the ongoing freeze ends
        and then a new one is started afterwards. *)

  type kinded_key =
    | Commit_t of commit_key
    | Node_t of node_key
    | Content_t of contents_key

  val layer_id : repo -> kinded_key -> layer_id Lwt.t
  (** [layer_id t kinded_key] returns the layer where an object, identified by
      its hash, is stored. *)

  val async_freeze : repo -> bool
  (** [async_freeze t] returns true if there is an ongoing freeze. To be used
      with caution, as a freeze can start (or stop) just after the test. It is
      helpful when a single freeze is called, to check whether it completed or
      not. *)

  val self_contained : ?min:commit list -> max:commit list -> repo -> unit Lwt.t
  (** [self_contained min max t] copies the commits in the range of [min, max]
      from lower into upper, in order to make the upper self contained. If [min]
      is missing then only the [max] commits are copied. *)

  val check_self_contained :
    ?heads:commit list ->
    repo ->
    ([> `Msg of string ], [> `Msg of string ]) result Lwt.t
  (** [check_self_contained ?heads] checks that the current upper layer of a
      store is self contained. *)

  val needs_recovery : repo -> bool
  (** [needs_recovery repo] detects if an ongoing freeze was interrupted during
      the last node crash. If it returns [true] then the next call to freeze
      needs to have its [recovery] flag set. *)

  (** These modules should not be used. They are exposed purely for testing
      purposes. *)
  module Backend_layer : sig
    module Hook : sig
      type 'a t

      val v : ('a -> unit Lwt.t) -> 'a t
    end

    val wait_for_freeze : repo -> unit Lwt.t

    val freeze' :
      ?min_lower:commit list ->
      ?max_lower:commit list ->
      ?min_upper:commit list ->
      ?max_upper:commit list ->
      ?recovery:bool ->
      ?hook:
        [ `After_Clear
        | `Before_Clear
        | `Before_Copy
        | `Before_Copy_Newies
        | `Before_Copy_Last_Newies
        | `Before_Flip ]
        Hook.t ->
      repo ->
      unit Lwt.t

    val upper_in_use : repo -> [ `Upper0 | `Upper1 ]
  end
end

module type S = sig
  type hash

  include
    Generic_key
      with type Schema.Hash.t = hash
       and type hash := hash
       and type contents_key = hash
       and type node_key = hash
       and type commit_key = hash
end

module Generic_key_is_a_generic_key (X : Generic_key) : Irmin.Generic_key.S = X
module S_is_an_S (X : S) : Irmin.S = X

(* Duplicated from [Irmin.Maker] in order to extend the body signature [S]. *)
module type Maker = sig
  type endpoint

  include Irmin.Key.Store_spec.Hash_keyed

  module Make (Schema : Irmin.Schema.S) :
    S
      with type hash = Schema.Hash.t
       and module Schema = Schema
       and type Backend.Remote.endpoint = endpoint
end

module Maker_is_a_maker (X : Maker) : Irmin.Maker = X

module type Sigs = sig
  module Layer_id : sig
    type t = layer_id [@@deriving irmin]

    val pp : Format.formatter -> t -> unit
    val to_string : t -> string
  end

  module type S = S
  module type Generic_key = Generic_key
  module type Maker = Maker

  module Maker
      (CA : Irmin.Content_addressable.Maker)
      (AW : Irmin.Atomic_write.Maker) : Maker

  module Stats = Stats
end
